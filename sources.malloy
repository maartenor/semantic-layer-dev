source: customers is duckdb.table('./data/customers.csv') extend {
    primary_key: customer_id
    measure: customer_cnt is count()
}

source: locations is duckdb.table('./data/locations.csv') extend {
    primary_key: location_id
    measure: location_cnt is count()
    join_one: customers on customers.customer_id = customer_id
}

source: machines is duckdb.table('./data/machines.csv') extend {
    primary_key: machine_nr
    measure: machine_cnt is count()
}

source: install_base is duckdb.table('./data/install_base.csv') extend {
    measure: installs_cnt is count()
    join_one: machines on machines.machine_nr = machine_nr
    join_one: locations on locations.location_id = location_id
    view: top_5_locations is {
        group_by: location_id
        aggregate: install_cnt is count()
        limit: 5
    }
    view: top_5_customers is {
        group_by: locations.customers.customer_name
        aggregate: machines_installed_cnt is count()
                   site_cnt is count(locations.site_name)
        limit: 5
    }
}  

source: states is duckdb.table('./data/states.csv') extend {
    timezone: 'America/Mexico_City'
    join_one: machines on machines.machine_nr = machine_nr
    measure:
        machine_cnt is count(machine_nr)        
        row_cnt is count()
        total_state_duration is sum(state_duration)
    dimension:
        state_duration is (hour(end_time) - hour(start_time))*60 
                        + (minute(end_time) - minute(start_time))
}

source: event_logs is duckdb.table('./data/event_logs.csv') extend {
    primary_key: event_id
    join_one: machines on machines.machine_nr = machine_nr
    measure: 
        event_cnt is count()
        machines_with_events_cnt is count(machine_nr) 
}

